Package org.deri.mins.parser;

/************************************************************
 * Helpers                                                  *
 ************************************************************/
Helpers 
    all = [0x0 .. 0xffff];
    //letter = [0x0030 .. 0x312C];
    letter = ['a' .. 'z'] | ['A' .. 'Z'];
    
    tab = 9;
    cr = 13;
    lf = 10;
    eol = cr lf | cr | lf;

    not_cr_lf = [all - [cr + lf]];
    short_comment = '//' not_cr_lf* eol;
    long_comment_content = [ all - '/' ] | [all - '*'] '/';
    long_comment    =   '/*' long_comment_content * '*/';
    comment = short_comment | long_comment;

    blank = (' ' | tab | eol )+;
    
    delim = ''';
    quoted_content = [ all - delim ] || '\' ''';
    quoted_id = delim quoted_content+ delim;

/************************************************************
 * Tokens                                                   *
 ************************************************************/  
Tokens
    t_blank = blank;
    t_comment = comment;
    t_dot = '.';
    t_impliedby = ':-';
    t_and = 'and';
    t_not = 'not' | '-' | 'naf' | '!';
    t_comma = ',';
    t_lpar = '(';
    t_rpar = ')';
    t_variable = '?' letter+;
    t_query = '?-';
    t_id = letter+ | quoted_id;
    

/************************************************************
 * Ignored Tokens                                           *
 ************************************************************/              
Ignored Tokens  
    t_blank,
    t_comment;  


/************************************************************
 * Productions                                              *
 ************************************************************/  
Productions
    program      = expr* ;
    
    expr         = {rule} predicate t_impliedby conjunction t_dot |
                   {fact} predicate t_dot |
                   {query} t_query conjunction t_dot;
    
    conjunction  = {body} body |
                   {and} conjunction t_and body |
                   {comma} conjunction t_comma body;
    
    body         = {negated} t_not predicate |
                   predicate;
                
    predicate    = t_id paramlist?;
    

    paramlist    = t_lpar termlist? t_rpar;

    termlist     = {term} term |
                   termlist t_comma term;
                   
    term         = t_id paramlist? | //constant or fsymbol
                   {var} t_variable;
   
